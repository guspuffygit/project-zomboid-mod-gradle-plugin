plugins {
	id 'java-gradle-plugin'
	id 'com.gradle.plugin-publish' version '2.0.0'
	id 'maven-publish'
	id 'com.diffplug.spotless' version '8.1.0'
}

spotless {
	java {
		cleanthat()
		googleJavaFormat().aosp().reflowLongStrings(false)
		removeUnusedImports()
		trimTrailingWhitespace()
		endWithNewline()
	}
	groovyGradle {
		target '*.gradle'
		greclipse()
	}
}

apply from: 'test.gradle'

version = '0.4.3'
group = 'io.pzstorm.capsid'

repositories {
	mavenCentral()
}

dependencies {
	implementation 'com.google.guava:guava:32.0.1-jre'

	compileOnly 'org.jetbrains:annotations:20.1.0'

	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
	testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

java {
	toolchain {
		languageVersion.set(JavaLanguageVersion.of(17))
	}
}

gradlePlugin {
	website = 'https://github.com/pzstorm/capsid'
	vcsUrl = 'https://github.com/pzstorm/capsid'

	plugins {
		register('capsid') {
			id = 'io.pzstorm.capsid'
			displayName = 'Idk'
			description = 'Project Zomboid mod development framework for Gradle'
			implementationClass = 'io.pzstorm.capsid.CapsidPlugin'
			tags = [
				'java',
				'project-zomboid',
				'modding',
				'framework',
			]
		}
	}
}

tasks.withType(Javadoc) {
	options.addStringOption('Xdoclint:none', '-quiet')
}

jar.from 'LICENSE'

tasks.withType(JavaCompile) {
	options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}
